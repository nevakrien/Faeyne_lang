use crate::lexer::LexTag;
use codespan::Span;
use crate::ast::*;//{StringTable,Value,FunctionCall,FuncSig,FuncBlock,Statment};

grammar<'input>(input: &'input str, table: &mut StringTable<'input>);

extern {
    type Location = usize;
    type Error = ();

    // Enum definitions with lexer output for basic tokens
    enum LexTag {
        "name" => LexTag::Name,
        "atom" => LexTag::Atom,
        "string" => LexTag::String,
        "poison_str" => LexTag::PoisonString,
        "UK" => LexTag::Unknowen,

        // Keywords
        "nil" => LexTag::Nil,
        "import" => LexTag::Import,
        "def" => LexTag::FuncDec,
        "fn" => LexTag::Lambda,
        "return" => LexTag::Return,

        // Numbers
        "float" => LexTag::Float(<f64>),
        "int" => LexTag::Int(<Result<i64,f64>>),

        // Operators and others
        "." => LexTag::Dot,
        ";" => LexTag::Ender,
        "," => LexTag::Comma,

        "(" => LexTag::OpenParen,
        ")" => LexTag::CloseParen,
        "{" => LexTag::OpenCurly,
        "}" => LexTag::CloseCurly,
        "[" => LexTag::OpenSquare,
        "]" => LexTag::CloseSquare,
        "|>" => LexTag::Pipe,

        "+" => LexTag::Plus,
        "-" => LexTag::Minus,
        "*" => LexTag::Mul,
        "/" => LexTag::Div,
        "//" => LexTag::IntDiv,
        "**" => LexTag::Pow,

        "%" => LexTag::Modolo,
        "&&" => LexTag::DoubleAnd,
        "||" => LexTag::DoubleOr,
        "^^" => LexTag::DoubleXor,

        "|" => LexTag::Or,
        "^" => LexTag::Xor,

        "=" => LexTag::Eq,
        "==" => LexTag::DoubleEq,
        "!=" => LexTag::NotEq,

        "<" => LexTag::Smaller,
        "<=" => LexTag::SmallerEq,
        ">" => LexTag::Bigger,
        ">=" => LexTag::BiggerEq,

        "=>" => LexTag::Arrow,
        "->" => LexTag::SmallArrow,
    }
}

//basic types:

Atom: usize = {
    <start: @L> "atom" <end: @R> => {
        table.get_id(&input[start..end])
    }
};

String: usize = {
    <start: @L> "string" <end: @R> => {
        table.get_id(&input[start..end])
    }
};

PoisonString: usize = {
    <start: @L> "poison_str" <end: @R> => {
        table.get_id(&input[start..end])
    }
};

Name: usize = {
    <start: @L> "name" <end: @R> => {
        table.get_id(&input[start..end])
    }
};

//functions
Lammda: Lammda = {
    "fn" <sig:ArgList> <body:FuncBlock> => Lammda {
        sig,
        body,
    },
    "fn" <sig:ArgList> "->" <body:FuncBlock> => Lammda {
        sig,
        body,
    }
};

pub FuncDec: FuncDec = {
    "def" <sig:FuncSig> <body:FuncBlock> => FuncDec {
        sig,
        body,
    }
};

pub FuncSig: FuncSig = {
    <name:Name> <args:ArgList> => FuncSig{name, args},
};

ArgList: Vec<usize> = {
    "(" <args:CommaSepArgs> ")" => args,

    "(" ")" => vec![],
};

CommaSepArgs: Vec<usize> = {
    <args:CommaSepArgs> "," <arg:Name> => {
        let mut arguments = args;
        arguments.push(arg);
        arguments
    },
    
    <arg:Name> => vec![arg]
};

CommaSepValue: Vec<Value> = {
    <args:CommaSepValue> "," <arg:Value> => {
        let mut arguments = args;
        arguments.push(arg);
        arguments
    },
    
    <arg:Value> => vec![arg],
};

//we need this to avoid ambiguty on things like a()|>c()() 
NoPipeFuncCall: FunctionCall = {
    #[precedence(level="0")]
    <start:@L> <name:Name> "(" <args:CommaSepValue> ")" <end:@R> => FunctionCall {
        name: FValue::Name(name),
        args: args,
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    },
    <start:@L> <name:Name> "(" ")" <end:@R> => FunctionCall {
        name: FValue::Name(name),
        args: vec![],
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    },

    <start:@L> <x:Lammda> "(" <args:CommaSepValue> ")" <end:@R> => FunctionCall {
        name: FValue::Lammda(Box::new(x)),
        args: args,
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    },
    <start:@L> <x:Lammda> "(" ")" <end:@R> => FunctionCall {
        name: FValue::Lammda(Box::new(x)),
        args: vec![],
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    },

    #[precedence(level="1")] 
    <start:@L> <func:NoPipeFuncCall> "(" <args:CommaSepValue> ")" <end:@R> => FunctionCall {
        name:  FValue::FuncCall(Box::new(func)),
        args: args,
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    },
    <start:@L> <func:NoPipeFuncCall> "(" ")" <end:@R> => FunctionCall {
        name: FValue::FuncCall(Box::new(func)),
        args: vec![],
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    },

};

Pipe: FunctionCall = {
    #[precedence(level="0")]
    <start:@L> <a:PipableValue> "|>" <b:NoPipeFuncCall> <end:@R> => {
        let mut new_args = vec![a];
        new_args.extend(b.args);
        FunctionCall {
            name: b.name,
            args: new_args,
            debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
        }
    },

    #[precedence(level="1")] //#[assoc(side="left")]
    <start:@L> <a:Pipe> "|>" <b:NoPipeFuncCall> <end:@R> => {
        let mut new_args = vec![Value::FuncCall(a)];
        new_args.extend(b.args);
        FunctionCall {
            name: b.name,
            args: new_args,
            debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
        }
    },
};

pub FuncCall: FunctionCall = {
    #[precedence(level="0")]
    <start:@L> <func:FuncCall> "(" <args:CommaSepValue> ")" <end:@R> => FunctionCall {
        name:  FValue::FuncCall(Box::new(func)),
        args: args,
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    },
    <start:@L> <func:FuncCall> "(" ")" <end:@R> => FunctionCall {
        name: FValue::FuncCall(Box::new(func)),
        args: vec![],
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    },

    #[precedence(level="1")]
    <b:NoPipeFuncCall> => b, // Span already included in NoPipeFuncCall

    #[precedence(level="2")]
    <p:Pipe> => p, // Pipe will also be updated for span
};



PipableValue: Value = {
    #[precedence(level="0")] 
    // Basic function calls can be piped
    <b:NoPipeFuncCall> => Value::FuncCall(b),
    "nil" => Value::Nil,

    #[precedence(level="1")] 
    // Simple names can be piped
    <a:Name> => Value::Variable(a),
    <x:Atom> => Value::Atom(x),
    <x:String> => Value::String(x),


    // numbers can be piped

    <x:"int"> => Value::Int(x),
    <x:"float"> => Value::Float(x),

    "-" <x:"int"> => Value::Int(match x {
        Ok(i) => Ok(-i),
        Err(f) => Err(-f),
    }),

    "-" <x:"float"> => Value::Float(-x),
};


//expressions
pub Value: Value = {
    #[precedence(level="10")] #[assoc(side="left")]
    <x:FuncCall> => Value::FuncCall(x),
    <x:Lammda> => Value::Lammda(Box::new(x)),

    #[precedence(level="0")]
    "nil" => Value::Nil,
    <x:Name> => Value::Variable(x),
    <x:Atom> => Value::Atom(x),
    <x:String> => Value::String(x),

    <x:"int"> => Value::Int(x),
    <x:"float"> => Value::Float(x),

    "-" <x:"int"> => Value::Int(match x {
        Ok(i) => Ok(-i),
        Err(f) => Err(-f),
    }),

    "-" <x:"float"> => Value::Float(-x),

    #[precedence(level="2")] #[assoc(side="left")]
    <start:@L> <a:Value> <op:MulOp> <b:Value> <end:@R> => Value::FuncCall(FunctionCall {
        name: FValue::BuildIn(op),
        args: vec![a, b],
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    }),

    #[precedence(level="3")] #[assoc(side="left")]
    <start:@L> <a:Value> <op:AddOp> <b:Value> <end:@R> => Value::FuncCall(FunctionCall {
        name: FValue::BuildIn(op),
        args: vec![a, b],
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    }),

    #[precedence(level="4")] #[assoc(side="left")]
    <start:@L> <a:Value> <op:LogicalOp> <b:Value> <end:@R> => Value::FuncCall(FunctionCall {
        name: FValue::BuildIn(op),
        args: vec![a, b],
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    }),
};


// Define operator groups for each precedence level
MulOp: BuildIn = {
    "*" => BuildIn::Mul,
    "/" => BuildIn::Div,
    "//" => BuildIn::IntDiv,
    "%" => BuildIn::Modulo,
    "**" => BuildIn::Pow, // Exponentiation
};

AddOp: BuildIn = {
    "+" => BuildIn::Add,
    "-" => BuildIn::Sub,
};

LogicalOp: BuildIn = {
    "==" => BuildIn::Equal,
    "<" => BuildIn::Smaller,
    ">" => BuildIn::Bigger,
    "<=" => BuildIn::SmallerEq,
    "&&" => BuildIn::DoubleAnd,
    "||" => BuildIn::DoubleOr,
    "^^" => BuildIn::DoubleXor,
    "|" => BuildIn::Or,
    "^" => BuildIn::Xor,
    //"|>" => BuildIn::Pipe, // Esoteric
};

Statment : Statment = {
    <func:FuncCall> ";" => Statment::Call(func),
    <n:Name> "=" <x:Value> ";" => Statment::Assign(n,x),
}

// Function block, with optional return statement
pub FuncBlock: FuncBlock = {
    "{" <body:Statements> <ret:Return> "}" => FuncBlock {
        body: body,
        ret: Some(ret),
    },

    "{" <body:Statements> "}" => FuncBlock {
        body: body,
        ret: None,
    },

    "{" <ret:Return> "}" => FuncBlock {
        body: vec![],
        ret: Some(ret),
    },

    "{" "}" => FuncBlock {
        body: vec![],
        ret: None,
    },
};


Statements: Vec<Statment> = {
    <stmts:Statements> <stmt:Statment> => {
        let mut statements = stmts;
        statements.push(stmt);
        statements
    },
    
    <stmt:Statment> => vec![stmt],
};

Return: Value = {
    "return" <val:Value> ";" => val,
    <val:Value> => val,
};

// match statments
//just sketching
ValueMatchArm : (Value,Value) = {
    <a:Value> "=>" <b:Value> => (a,b),
}