use crate::lexer::LexTag;
use codespan::Span;
use crate::ast::*;//{StringTable,Value,FunctionCall,FuncSig,FuncBlock,Statment};

grammar<'input>(input: &'input str, table: &mut StringTable<'input>);

extern {
    type Location = usize;
    type Error = ();

    // Enum definitions with lexer output for basic tokens
    enum LexTag {
        "name" => LexTag::Name,
        "atom" => LexTag::Atom,
        "string" => LexTag::String,
        "true" => LexTag::True,
        "false" => LexTag::False,
        "poison_str" => LexTag::PoisonString,
        "UK" => LexTag::Unknowen,

        // Keywords
        "nil" => LexTag::Nil,
        "import" => LexTag::Import,
        "from" => LexTag::From,
        "def" => LexTag::FuncDec,
        "fn" => LexTag::Lambda,
        "return" => LexTag::Return,
        "match" => LexTag::Match,

        // Numbers
        "float" => LexTag::Float(<f64>),
        "int" => LexTag::Int(<Result<i64,f64>>),

        // Operators and others
        "." => LexTag::Dot,
        ";" => LexTag::Ender,
        "," => LexTag::Comma,

        "(" => LexTag::OpenParen,
        ")" => LexTag::CloseParen,
        "{" => LexTag::OpenCurly,
        "}" => LexTag::CloseCurly,
        "[" => LexTag::OpenSquare,
        "]" => LexTag::CloseSquare,
        "|>" => LexTag::Pipe,

        "+" => LexTag::Plus,
        "-" => LexTag::Minus,
        "*" => LexTag::Mul,
        "/" => LexTag::Div,
        "//" => LexTag::IntDiv,
        "**" => LexTag::Pow,

        "%" => LexTag::Modolo,

        "&&" => LexTag::DoubleAnd,
        "||" => LexTag::DoubleOr,
        "^^" => LexTag::DoubleXor,

        "&" => LexTag::And,
        "|" => LexTag::Or,
        "^" => LexTag::Xor,
        

        "=" => LexTag::Eq,
        "==" => LexTag::DoubleEq,
        "!=" => LexTag::NotEq,

        "<" => LexTag::Smaller,
        "<=" => LexTag::SmallerEq,
        ">" => LexTag::Bigger,
        ">=" => LexTag::BiggerEq,

        "=>" => LexTag::Arrow,
        "->" => LexTag::SmallArrow,
    }
}

//### basic types:

Atom: usize = {
    <start: @L> "atom" <end: @R> => {
        table.get_id(&input[start..end])
    }
};

String: usize = {
    <start: @L> "string" <end: @R> => {
        table.get_id(&input[start..end])
    }
};

PoisonString: usize = {
    <start: @L> "poison_str" <end: @R> => {
        table.get_id(&input[start..end])
    }
};

Name: usize = {
    <start: @L> "name" <end: @R> => {
        table.get_id(&input[start..end])
    }
};



//### function calls:
CommaSepValue: Vec<Value> = {
    <args:CommaSepValue> "," <arg:Value> => {
        let mut arguments = args;
        arguments.push(arg);
        arguments
    },
    
    <arg:Value> => vec![arg],
};

//we need this to avoid ambiguty on things like a()|>c()() 
NoPipeFuncCall: FunctionCall = {
    #[precedence(level="0")]
    <start:@L> <name:Name> "(" <args:CommaSepValue> ")" <end:@R> => FunctionCall {
        name: FValue::Name(name),
        args: args,
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    },
    <start:@L> <name:Name> "(" ")" <end:@R> => FunctionCall {
        name: FValue::Name(name),
        args: vec![],
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    },

    <start:@L> <x:Lambda> "(" <args:CommaSepValue> ")" <end:@R> => FunctionCall {
        name: FValue::Lambda(Box::new(x)),
        args: args,
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    },
    <start:@L> <x:Lambda> "(" ")" <end:@R> => FunctionCall {
        name: FValue::Lambda(Box::new(x)),
        args: vec![],
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    },

    #[precedence(level="1")] 
    <start:@L> <func:NoPipeFuncCall> "(" <args:CommaSepValue> ")" <end:@R> => FunctionCall {
        name:  FValue::FuncCall(Box::new(func)),
        args: args,
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    },
    <start:@L> <func:NoPipeFuncCall> "(" ")" <end:@R> => FunctionCall {
        name: FValue::FuncCall(Box::new(func)),
        args: vec![],
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    },

};

Pipe: FunctionCall = {
    #[precedence(level="0")]
    <start:@L> <a:PipableValue> "|>" <b:NoPipeFuncCall> <end:@R> => {
        let mut new_args = vec![a];
        new_args.extend(b.args);
        FunctionCall {
            name: b.name,
            args: new_args,
            debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
        }
    },

    #[precedence(level="1")] //#[assoc(side="left")]
    <start:@L> <a:Pipe> "|>" <b:NoPipeFuncCall> <end:@R> => {
        let mut new_args = vec![Value::FuncCall(a)];
        new_args.extend(b.args);
        FunctionCall {
            name: b.name,
            args: new_args,
            debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
        }
    },
};

pub FuncCall: FunctionCall = {
    #[precedence(level="0")]
    <start:@L> <func:FuncCall> "(" <args:CommaSepValue> ")" <end:@R> => FunctionCall {
        name:  FValue::FuncCall(Box::new(func)),
        args: args,
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    },
    <start:@L> <func:FuncCall> "(" ")" <end:@R> => FunctionCall {
        name: FValue::FuncCall(Box::new(func)),
        args: vec![],
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    },

    #[precedence(level="1")]
    <b:NoPipeFuncCall> => b, // Span already included in NoPipeFuncCall

    #[precedence(level="2")]
    <p:Pipe> => p, // Pipe will also be updated for span
};

Literal : Literal = {
    "nil" => Literal::Nil,
    "true" => Literal::Bool(true),
    "false" => Literal::Bool(false),


    <x:Atom> => Literal::Atom(x),
    <x:String> => Literal::String(x),

    <x:"int"> => Literal::Int(x),
    <x:"float"> => Literal::Float(x),

    "-" <x:"int"> => Literal::Int(match x {
        Ok(i) => Ok(-i),
        Err(f) => Err(-f),
    }),

    "-" <x:"float"> => Literal::Float(-x),
}

PipableValue: Value = {
    #[precedence(level="0")] 
    // Basic function calls can be piped
    <b:NoPipeFuncCall> => Value::FuncCall(b),

    #[precedence(level="1")] 
    // Simple names can be piped
    <a:Name> => Value::Variable(a),
    <l:Literal> => l.into(),
    <m:MatchStatment>  => Value::Match(m),
};


//### expressions
pub Value: Value = {
    #[precedence(level="10")] #[assoc(side="left")]
    <x:FuncCall> => Value::FuncCall(x),
    <x:Lambda> => Value::Lambda(Box::new(x)),
    <m:MatchLambda>  => Value::MatchLambda(Box::new(m)),
    
    #[precedence(level="0")]
    <x:Name> => Value::Variable(x),
    <l:Literal> => l.into(),
    <m:MatchStatment>  => Value::Match(m),


    // Parentheses should have the highest precedence
    #[precedence(level="1")] 
    <a:OpenExpr> ")" => a,

    // Exponentiation (Power) should have higher precedence than multiplication
    #[precedence(level="2")] #[assoc(side="right")]
    <start:@L> <a:Value> <op:PowerOp> <b:Value> <end:@R> => Value::FuncCall(FunctionCall {
        name: FValue::BuildIn(op),
        args: vec![a, b],
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    }),

    #[precedence(level="3")] #[assoc(side="left")]
    <start:@L> <a:Value> <op:MulOp> <b:Value> <end:@R> => Value::FuncCall(FunctionCall {
        name: FValue::BuildIn(op),
        args: vec![a, b],
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    }),

    #[precedence(level="4")] #[assoc(side="left")]
    <start:@L> <a:Value> <op:AddOp> <b:Value> <end:@R> => Value::FuncCall(FunctionCall {
        name: FValue::BuildIn(op),
        args: vec![a, b],
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    }),

    // Bitwise XOR and OR should be higher precedence than comparison but lower than arithmetic
    #[precedence(level="5")] #[assoc(side="left")]
    <start:@L> <a:Value> <op:BitwiseOp> <b:Value> <end:@R> => Value::FuncCall(FunctionCall {
        name: FValue::BuildIn(op),
        args: vec![a, b],
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    }),

    // Comparison operators should have higher precedence than logical operators
    #[precedence(level="6")] #[assoc(side="left")]
    <start:@L> <a:Value> <op:ComparisonOp> <b:Value> <end:@R> => Value::FuncCall(FunctionCall {
        name: FValue::BuildIn(op),
        args: vec![a, b],
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    }),

    #[precedence(level="7")] #[assoc(side="left")]
    <start:@L> <a:Value> <op:LogicalOp> <b:Value> <end:@R> => Value::FuncCall(FunctionCall {
        name: FValue::BuildIn(op),
        args: vec![a, b],
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    }),
};

OpenExpr : Value = {
    "(" <v:Value> => v
};

// Define operator groups for each precedence level
PowerOp: BuildIn = {
    "**" => BuildIn::Pow, // Exponentiation
};

MulOp: BuildIn = {
    "*" => BuildIn::Mul,
    "/" => BuildIn::Div,
    "//" => BuildIn::IntDiv,
    "%" => BuildIn::Modulo,
};

AddOp: BuildIn = {
    "+" => BuildIn::Add,
    "-" => BuildIn::Sub,
};

// Bitwise XOR and OR (level 5)
BitwiseOp: BuildIn = {
    "^" => BuildIn::Xor,
    "|" => BuildIn::Or,
};

// Comparison operators (level 6)
ComparisonOp: BuildIn = {
    "==" => BuildIn::Equal,
    "!=" => BuildIn::NotEqual,
    "<" => BuildIn::Smaller,
    ">" => BuildIn::Bigger,
    "<=" => BuildIn::SmallerEq,
    ">=" => BuildIn::BiggerEq,
};

// Logical operators (level 7)
LogicalOp: BuildIn = {
    "&&" => BuildIn::DoubleAnd,
    "||" => BuildIn::DoubleOr,
    "^^" => BuildIn::DoubleXor,
};




//### function defs
pub FuncDec: FuncDec = {
    "def" <sig:FuncSig> <body:FuncBlock> => FuncDec {
        sig,
        body,
    }
};

Lambda: Lambda = {
    "fn" <sig:ArgList> <body:FuncBlock> => Lambda {
        sig,
        body,
    },
    "fn" <sig:ArgList> "->" <body:FuncBlock> => Lambda {
        sig,
        body,
    }
};



pub FuncSig: FuncSig = {
    <name:Name> <args:ArgList> => FuncSig{name, args},
};

ArgList: Vec<usize> = {
    "(" <args:CommaSepArgs> ")" => args,

    "(" ")" => vec![],
};

CommaSepArgs: Vec<usize> = {
    <args:CommaSepArgs> "," <arg:Name> => {
        let mut arguments = args;
        arguments.push(arg);
        arguments
    },
    
    <arg:Name> => vec![arg]
};

pub FuncBlock: FuncBlock = {
    "{" <body:Statements> <ret:Return> "}" => FuncBlock {
        body: body,
        ret: Some(ret),
    },

    "{" <body:Statements> "}" => FuncBlock {
        body: body,
        ret: None,
    },

    "{" <ret:Return> "}" => FuncBlock {
        body: vec![],
        ret: Some(ret),
    },

    "{" "}" => FuncBlock {
        body: vec![],
        ret: None,
    },
};


Statment : Statment = {
    <func:FuncCall> ";" => Statment::Call(func),
    <m:MatchStatment> ";" => Statment::Match(m),
    <n:Name> "=" <x:Value> ";" => Statment::Assign(n,x),
}

Statements: Vec<Statment> = {
    <stmts:Statements> <stmt:Statment> => {
        let mut statements = stmts;
        statements.push(stmt);
        statements
    },
    
    <stmt:Statment> => vec![stmt],
};

Return: Ret = {
    "return" <val:Value> ";" => Ret::Exp(val),
    <val:Value> => Ret::Imp(val),
};

//### match statments
MatchOut : MatchOut = {
    <v:Value> => MatchOut::Value(v),
    <b:FuncBlock> => MatchOut::Block(b),
};

MatchPattern : MatchPattern = {
    <l:Literal> => MatchPattern::Literal(l), 
    <n:Name> => match table.compare_to(n,"_") {
            true => MatchPattern::Wildcard,
            false => MatchPattern::Variable(n),
    },
    
};


MatchArm : MatchArm = {
    <pattern:MatchPattern> "=>" <result:MatchOut> => MatchArm{pattern,result}
};

OpenMatchArms: Vec<MatchArm> = {
    <v:OpenMatchArms> "," <arm:MatchArm> => {
        let mut v = v;
        v.push(arm);
        v
    },
    
    "{" <arm:MatchArm> => vec![arm],
};

MatchArms : Vec<MatchArm> = {
    <arms:OpenMatchArms> "," "}"=> arms,
    <arms:OpenMatchArms> "}"=> arms,
};

pub MatchStatment:MatchStatment = {
  <start:@L> "match" <val:Value> <arms:MatchArms> <end:@R> => MatchStatment {
        val: Box::new(val),
        arms,
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    }, 
};

pub MatchLambda:MatchLambda = {
  <start:@L> "match" "fn" <arms:MatchArms> <end:@R> => MatchLambda {
        arms,
        debug_span: Span::new(start as u32, end as u32) // Capture span from start to end
    }, 
};

//modules
ImportFunc : ImportFunc = {
    "from" <path:Name> "import" <name:Name> => ImportFunc{path,name},
    "from" <path:Name> "import" <name:Name> ";"=> ImportFunc{path,name},
}

pub Program : Vec<OuterExp> = {
    <p:Program>  <f:ImportFunc> => {
        let mut program = p;
        program.push(OuterExp::ImportFunc(f));
        program
    },

    <p:Program>  <f:FuncDec> => {
        let mut program = p;
        program.push(OuterExp::FuncDec(f));
        program
    },
    
    <f:ImportFunc> => vec![OuterExp::ImportFunc(f)],
    <f:FuncDec> => vec![OuterExp::FuncDec(f)],
}
